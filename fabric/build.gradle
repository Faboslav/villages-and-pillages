plugins {
	id "com.github.johnrengelman.shadow" version "7.1.2"
}

allprojects {
	archivesBaseName = "friendsandfoes-beekeeperhut-fabric-mc${rootProject.minecraft_version}"
}

architectury {
	platformSetupLoomIde()
	fabric()
}

loom {
	accessWidenerPath = project(":common").loom.accessWidenerPath
}

configurations {
	common
	shadowCommon
	compileClasspath.extendsFrom common
	runtimeClasspath.extendsFrom common
	developmentFabric.extendsFrom common
}

dependencies {
	// Core dependencies
	modImplementation "net.fabricmc:fabric-loader:${rootProject.fabric_loader_version}"
	modApi "net.fabricmc.fabric-api:fabric-api:${rootProject.fabric_api_version}"
	modApi "com.yungnickyoung.minecraft.yungsapi:YungsApi:${minecraft_version}-Fabric-${yungsapi_version}"

	// For faster development https://www.cursemaven.com/
	// https://www.curseforge.com/minecraft/mc-mods/skip-transitions/files
	modRuntimeOnly "curse.maven:skip-transitions-592071:3735539"
	// https://www.curseforge.com/minecraft/mc-mods/just-load
	//modRuntimeOnly "curse.maven:just-load-601990:3921149"
	// https://www.curseforge.com/minecraft/mc-mods/fastload
	//modRuntimeOnly "curse.maven:fastload-630620:4278866"
	// https://www.curseforge.com/minecraft/mc-mods/lazydfu/files
	modRuntimeOnly "curse.maven:lazydfu-433518:3821870"
	// https://www.curseforge.com/minecraft/mc-mods/starlight/files
	modRuntimeOnly "curse.maven:lithium-360438:4151762"
	//modRuntimeOnly "curse.maven:sodium-394468:3957319"
	modRuntimeOnly "curse.maven:starlight-521783:3835973"
	//modRuntimeOnly "curse.maven:async-locator-625069:4257310"
	modRuntimeOnly "curse.maven:disablecustomworldsadvice-401978:3827588"

	common(project(path: ":common", configuration: "namedElements")) { transitive false }
	shadowCommon(project(path: ":common", configuration: "transformProductionFabric")) { transitive false }
}

processResources {
	inputs.property 'modId', rootProject.mod_id
	inputs.property 'modName', rootProject.mod_name
	inputs.property 'modDescription', rootProject.mod_description
	inputs.property 'modVersion', rootProject.mod_version
	inputs.property 'modAuthor', rootProject.mod_author
	inputs.property "minecraftVersion", rootProject.minecraft_version
	inputs.property "yungsapiVersion", rootProject.yungsapi_version

	filesMatching("fabric.mod.json") {
		expand([
			'modId'         : rootProject.mod_id,
			'modName'       : rootProject.mod_name,
			'modDescription': rootProject.mod_description,
			'modVersion'    : rootProject.mod_version,
			'modAuthor'     : rootProject.mod_author,
			"minecraftVersion": rootProject.minecraft_version,
			"yungsapiVersion": rootProject.yungsapi_version
		])
	}
}

shadowJar {
	exclude "architectury.common.json"

	configurations = [project.configurations.shadowCommon]
	classifier "dev-shadow"
}

remapJar {
	injectAccessWidener = true
	input.set shadowJar.archiveFile
	dependsOn shadowJar
	classifier null
}

jar {
	classifier "dev"
}

sourcesJar {
	def commonSources = project(":common").sourcesJar
	dependsOn commonSources
	from commonSources.archiveFile.map { zipTree(it) }
}

components.java {
	withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
		skip()
	}
}

repositories {
	maven {
		name = 'Fabric'
		url = 'https://maven.fabricmc.net/'
	}

	maven {
		name = "Modrinth"
		url = "https://api.modrinth.com/maven"

		content {
			includeGroup "maven.modrinth"
		}
	}

	maven {
		url "https://maven.terraformersmc.com/releases/"
		content {
			includeGroup "com.terraformersmc"
		}
	}

	maven { url "https://www.cursemaven.com" }
}