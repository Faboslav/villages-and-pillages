plugins {
	id "com.github.johnrengelman.shadow" version "7.1.2"
}

allprojects {
	archivesBaseName = "villagesandpillages-fabric-mc${rootProject.minecraft_version}"
}

configurations.all {
	resolutionStrategy {
		force "net.fabricmc:fabric-loader:${rootProject.fabric_loader_version}"
	}
}

architectury {
	platformSetupLoomIde()
	fabric()
}

loom {
	accessWidenerPath = project(":common").loom.accessWidenerPath
}

configurations {
	common
	shadowCommon
	compileClasspath.extendsFrom common
	runtimeClasspath.extendsFrom common
	developmentFabric.extendsFrom common
}

dependencies {
	// Core dependencies
	modImplementation "net.fabricmc:fabric-loader:${rootProject.fabric_loader_version}"
	modApi "net.fabricmc.fabric-api:fabric-api:${rootProject.fabric_api_version}"
	modApi "com.yungnickyoung.minecraft.yungsapi:YungsApi:${yungsapi_mc_version}-Fabric-${yungsapi_version}"

	// For faster development https://www.cursemaven.com/
	// https://www.curseforge.com/minecraft/mc-mods/skip-transitions/files
	modRuntimeOnly "curse.maven:skip-transitions-592071:4628108"
	// https://www.curseforge.com/minecraft/mc-mods/fastload
	modRuntimeOnly "curse.maven:fastload-630620:4706149"
	// https://www.curseforge.com/minecraft/mc-mods/lazydfu/files
	modRuntimeOnly "curse.maven:lazydfu-433518:3821870"
	// https://www.curseforge.com/minecraft/mc-mods/fabric-disable-custom-worlds-advice/files
	modRuntimeOnly "curse.maven:disablecustomworldsadvice-401978:4619700"

	// Integrations
	// Waystones
	modRuntimeOnly "curse.maven:cloth-config-348521:4633442"
	modRuntimeOnly "curse.maven:balm-fabric-500525:4757221"
	modRuntimeOnly "curse.maven:waystones-fabric-500087:4749831"

	common(project(path: ":common", configuration: "namedElements")) { transitive false }
	shadowCommon(project(path: ":common", configuration: "transformProductionFabric")) { transitive false }
}

processResources {
	inputs.property 'modId', rootProject.mod_id
	inputs.property 'modName', rootProject.mod_name
	inputs.property 'modDescription', rootProject.mod_description
	inputs.property 'modVersion', rootProject.mod_version
	inputs.property 'modAuthor', rootProject.mod_author
	inputs.property "minecraftVersion", rootProject.minecraft_version

	filesMatching("fabric.mod.json") {
		expand([
			'modId'         : rootProject.mod_id,
			'modName'       : rootProject.mod_name,
			'modDescription': rootProject.mod_description,
			'modVersion'    : rootProject.mod_version,
			'modAuthor'     : rootProject.mod_author,
			"minecraftVersion": rootProject.minecraft_version,
		])
	}
}

shadowJar {
	exclude "architectury.common.json"

	configurations = [project.configurations.shadowCommon]
	archiveClassifier.set("dev-shadow")
}

remapJar {
	injectAccessWidener = true
	input.set shadowJar.archiveFile
	dependsOn shadowJar
	archiveClassifier.set(null)
}

jar {
	archiveClassifier.set("dev")
}

sourcesJar {
	def commonSources = project(":common").sourcesJar
	dependsOn commonSources
	from commonSources.archiveFile.map { zipTree(it) }
}

components.java {
	withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
		skip()
	}
}

repositories {
	maven {
		name = 'Fabric'
		url = 'https://maven.fabricmc.net/'
	}

	maven {
		name = "Modrinth"
		url = "https://api.modrinth.com/maven"

		content {
			includeGroup "maven.modrinth"
		}
	}

	maven {
		url "https://maven.terraformersmc.com/releases/"
		content {
			includeGroup "com.terraformersmc"
		}
	}

	maven { url "https://www.cursemaven.com" }
}